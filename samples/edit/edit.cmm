/*
 * 簡単なテキストエディタ
 * （IE3 データ構造 2001.7.6 の課題をベースに C-- 版に書き換えた）
 *
 * 2018.11.26 : 初期バージョン
 */
#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#ifndef C
#include <syslib.hmm>                    // TaC版は一部のシステムコールを使う
#endif

#define  LINES  300               /* 編集できる文書は最大 300 行 */
#define  CHARS  81                /* 編集できる文書の１行は最大 80 文字 */
#define  BUFLEN 100               /* ファイル名や命令の長さは99文字以内 */

/*
 * グローバル変数
 */
char[][] textbuf=array(LINES,CHARS);                  // 文書を記憶するバッファ
int    lines;                                         // 文書が全部で何行あるか
int    current;                                       // 現在第何行にいるか
char[] kbdBuf=array(BUFLEN);                          // キーボード入力用

/*
 * 1行を入力する関数(サブルーチン) 行末の改行は取り去る
 *     buf    : 1行を格納する文字配列
 *     buflen : buf に渡される文字配列のサイズ
 *     fp     : オープン済のファイルポインタ
 *              stdin が渡されるとキーボードから入力する
 */
boolean getLine(char[] buf, int buflen, FILE fp) {
  if (fgets(buf, buflen, fp)==null) {                 // EOF
    return false;
  }
  if (strLen(buf)>=buflen) {
    fprintf(stderr, "too long line\n");
    return false;                                     // EOF
  }
  int pos =(strRchr(buf, '\n'));                      // 行末に'\n'を探す
  if (pos>=0) {                                       //   あれば
    buf[pos] = '\0';                                  //     削除する
  }
  return true;                                        // 正常に入力できた
}

/*
 * ファイルから文書を textbuf へ読み込む関数(サブルーチン)
 */
void loadfile(char[] filename) {
  FILE fp=fopen(filename,"r");                        // ファイルを開く
  lines = 0;                                          // バッファを空にする
  if (fp==null) {                                     // オープン失敗
    perror(filename);
  } else {                                            // オープン成功！！
    while(getLine( textbuf[lines], CHARS, fp)) {      //   EOF になるまで
      lines = lines + 1;                              //    ファイルから入力
    }
    fclose(fp);                                       //   忘れず閉じる
  }
}

/*
 * textbuf の文書をファイルへ保存する関数(サブルーチン)
 */
void savefile(char[] filename) {
#ifndef C
  remove(filename);                                   // TaC版では必要
#endif
  FILE fp=fopen(filename,"w");                        // ファイルを開く
  if (fp==null) {                                     // オープン失敗
    perror(filename);
    return;
  }
  for (current=0; current<lines; current=current+1) {
    fprintf(fp,"%s\n",textbuf[current]);
  }
  fclose(fp);
}

/*
 * プリントコマンドを実行する関数(サブルーチン)
 *     プリントコマンドは現在行の行番号と内容を表示する
 */
void print() {
  if (current!=lines) {
    fprintf(stderr,"[%d] : %s\n",current,textbuf[current]);
  } else {
    fprintf(stderr,"[%d] : \n",current);
  }
}


/*
 * リストコマンドを実行する関数(サブルーチン)
 *     リストコマンドは現在行から最後まで表示する
 */
void list() {
  for (int i=current; i<lines; i=i+1) {
    fprintf(stderr,"[%d] : %s\n",i,textbuf[i]);
  }
}

/*
 * ジャンプコマンドを実行する関数(サブルーチン)
 *     ジャンプコマンドは現在行を変更する
 */
void jump() {
  fprintf(stderr, "line number = ");
  getLine(kbdBuf,BUFLEN,stdin);                       // ジャンプ先の行番号
  current=atoi(kbdBuf);                               // 文字列"10" => 整数10
  if (current<0) current=0;                           // ジャンプできる範囲？
  if (current>lines) current=lines;
  print();                                            // ジャンプ結果を表示
}

/*
 * チェンジコマンドを実行する関数(サブルーチン)
 *     チェンジコマンドは行の内容を変更する
 */
void change() {
  if (current!=lines) {
    fprintf(stderr,"%s\n",textbuf[current]);          // 現在の状態を出力
    fprintf(stderr,"input new content ");
    getLine(textbuf[current],CHARS,stdin);            // 新しい内容を入力
  } else {
    fprintf(stderr,"current line = EOF\n");
  }
}

/*
 * １行挿入
 */
void insert() {
  int i;
  for (i=lines; i>current; i=i+1)
    strCpy(textbuf[i], textbuf[i-1]);
  fprintf(stderr, "new line ");
  getLine(textbuf[current],CHARS,stdin);              // 新しい行を入力
  lines = lines + 1;
  current = current + 1;
}

/*
 * 1行削除
 */
void delete() {
  int i;
  if (current!=lines) {
    for (i=current+1; i<lines; i=i+1)
      strCpy(textbuf[i-1], textbuf[i]);
    lines=lines-1;
  } else {
    fprintf(stderr,"current line = EOF\n");
  }
}

/*
 * ヘルプ
 */
void help() {
  fprintf(stderr,"j : jump\n");
  fprintf(stderr,"p : print\n");
  fprintf(stderr,"l : list\n");
  fprintf(stderr,"c : change line content\n");
  fprintf(stderr,"i : insert line\n");
  fprintf(stderr,"d : delete line\n");
  fprintf(stderr,"q : quit\n");
 }

/*
 * エディタプログラムのメインルーチン
 */
public int main(int argc, char[][] argv) {
  if (argc!=2) {
    fprintf(stderr, "Usage: %s <filename>\n", argv[0]);
    return 1;
  }
  char[] filename=argv[1];
  loadfile(filename);
  current = 0;                                        // 現在行は1行目

  while(true) {
    fprintf(stderr,"[%d] command: ",current);
    if (!getLine(kbdBuf,BUFLEN,stdin)) {    // EOF
      break;
    } else if (strCmp(kbdBuf,"j")==0) {     // j: ジャンプコマンド
      jump();
    } else if (strCmp(kbdBuf,"p")==0) {     // p: プリントコマンド
      print();
    } else if (strCmp(kbdBuf,"l")==0) {     // l: リストコマンド
      list();
    } else if (strCmp(kbdBuf,"c")==0) {     // c: 変更コマンド
      change();
    } else if (strCmp(kbdBuf,"q")==0) {     // q: quit(終了)コマンド
      break;
    } else if (strCmp(kbdBuf,"i")==0) {     // i: １行挿入コマンド
      insert();
    } else if (strCmp(kbdBuf,"d")==0) {     // d: １行削除)コマンド
      delete();
    } else {
      help();
    }
  }
  savefile(filename);
  return 0;
}
